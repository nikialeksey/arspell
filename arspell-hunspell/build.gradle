apply plugin: 'java-library'
apply plugin: 'kotlin'
apply plugin: 'jacoco'

sourceCompatibility = 1.8
targetCompatibility = 1.8

dependencies {
    implementation project(':arspell')
    api 'com.atlascopco:hunspell-bridj:1.0.4'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    testImplementation 'junit:junit:4.12'
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = false
    }
}

check.dependsOn jacocoTestReport

ext {
    bintrayName = 'arspell-hunspell'
    artifact = 'arspell-hunspell'
    libraryName = 'arspell-hunspell'
}
if (project.rootProject.file('local.properties').exists()) {
    apply plugin: 'maven'
    group = publishedGroupId
    install {
        repositories.mavenInstaller {
            pom.project {
                packaging 'jar'
                groupId publishedGroupId
                artifactId artifact

                name libraryName
                description libraryDescription
                url siteUrl

                licenses {
                    license {
                        name licenseName
                        url licenseUrl
                    }
                }
                developers {
                    developer {
                        id developerId
                        name developerName
                        email developerEmail
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }

    apply from: 'https://raw.githubusercontent.com/nuuneoi/JCenter/master/bintrayv1.gradle'
    apply plugin: 'maven-publish'
    publishing {
        publications {
            arspell_hunspell(MavenPublication) {
                artifact(sourcesJar)
                artifact("$buildDir/libs/arspell-hunspell-${libraryVersion}.jar")

                groupId publishedGroupId
                artifactId artifact
                version libraryVersion

                pom.withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', 'com.atlascopco')
                    dependencyNode.appendNode('artifactId', 'hunspell-bridj')
                    dependencyNode.appendNode('version', '1.0.4')
                }
            }
        }
    }
    bintray.publications = ['arspell_hunspell']
    bintray.configurations = []
}
